plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group = 'org.example'
version = findProperty('version') ?: '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
}

application {
    mainClassName = 'org.example.TodoApp'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName.set('todo-app')
    archiveClassifier.set('')
    archiveVersion.set(project.version.toString())
    mergeServiceFiles()
}

jar {
    enabled = false
}

tasks.build {
    dependsOn shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.example'
            artifactId = 'todo-app'
            version = project.version

            artifact(tasks.shadowJar) {
                classifier = null
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Ehson011/todo-app")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}